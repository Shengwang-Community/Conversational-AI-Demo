plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}
apply from: 'resource.gradle'

def buildTimestamp = new Date().format("yyyyMMddHHmm")

static def generateVersionCode() {
    def date = new Date()
    return Integer.parseInt(date.format("yyMMddHH"))
}

if (project.getProperty("AG_APP_ID").toString().isEmpty()) {
    throw new GradleException("Please configure Agora ID and Certificate in gradle.properties")
}

android {
    namespace 'io.agora.agent'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        ndk.abiFilters 'arm64-v8a', 'armeabi-v7a'
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 26012120
        versionName "2.1.0"
        
        buildConfigField("String", "BUILD_TIMESTAMP", "\"${buildTimestamp}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "region"

    productFlavors {
        china {
            dimension "region"
            applicationId "cn.shengwang.convoai"
            resValue "string", "app_name", "对话式 AI 引擎"
            buildConfigField("String", "TOOLBOX_SERVER_HOST", "\"${project.getProperty("TOOLBOX_SERVER_HOST")}\"")
            buildConfigField("String", "AG_APP_ID", "\"${project.getProperty("AG_APP_ID")}\"")
            buildConfigField("String", "AG_APP_CERTIFICATE", "\"${project.getProperty("AG_APP_CERTIFICATE")}\"")
        }
    }

    signingConfigs {
        release {
            storeFile new File(rootProject.rootDir.absolutePath + "/keystore.key")
            storePassword "965606"
            keyAlias "agora"
            keyPassword "965606"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // Override versionCode for release builds
            if (variant.buildType.name == "release") {
                output.versionCodeOverride = generateVersionCode()
            }
            
            def flavorPrefix = "ShengWang_Conversational_Al_Engine_Demo_for_Android"

            // Get package name and replace dots with underscores for filename compatibility
            def packageName = variant.applicationId.replaceAll('\\.', '_')
            
            // APK filename includes package name, environment info for easy identification
            outputFileName = new File(
                    flavorPrefix + "_" +
                            packageName + "_" +
                            "v" + defaultConfig.versionName + "_" +
                            buildTimestamp + ".apk")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':scenes:convoai')

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}